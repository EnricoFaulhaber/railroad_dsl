WHITESPACE  = _{ " " | "\t" | "\n" }

term        = @{ "\"" ~ ("\\" ~ ANY | !"\"" ~ ANY)* ~ "\"" }
nonterm     = @{ "'"  ~ ("\\" ~ ANY | !"'" ~ ANY)*  ~ "'" }
comment     = @{ "`"  ~ ("\\" ~ ANY | !"`" ~ ANY)*  ~ "`" }
empty       = @{ "!" }

sequence    =  { "[" ~ comma_list ~ "]" }
sep         =  { "(" ~ comma_list ~ ")" }
stack       =  { "{" ~ comma_list ~ "}" }
choice      =  { "<" ~ comma_list ~ ">" }
comma_list  = _{ lbox_expr ~ (","? ~ lbox_expr)* }

simple_expr = _{ term | nonterm | comment | sequence | stack | choice | empty | sep }
qmark       =  { "?" }
opt_expr    =  { simple_expr ~ qmark? }
rpt_expr    =  { opt_expr ~ ("*" ~ opt_expr)? }

lbox_expr   =  { rpt_expr ~ ("#" ~ rpt_expr)? }

root_expr   =  { (sep | empty | lbox_expr)+ }
input       = _{ root_expr ~ EOI }


